#### Making Things Move

Cool, so there's a ball on the screen. That's nice, but I want to see it doing
something. So let's make some changes to that Main function, so make it do
something. In the OBJ block, add this line to include the LameControl library
in your project.

    
    
        ctrl     : "LameControl"  

Below ` gfx.Start ` , add ` ctrl.Start ` , which sets up the ` LameControl `
library so you can use the buttons and joystick in your games.

    
    
        lcd.Start(gfx.Start)
        ctrl.Start

Then, indent your `gfx.Sprite` and `lcd.Draw` commands and stick
them inside a `repeat` command. This will cause the drawing commands to
repeat forever.

    
    
        repeat
            gfx.Sprite(ball.Addr, 10, 10, 0)
            lcd.Draw

Now, inside VAR block, add some variables to work with. We need these for
movement, for you and the opponent later on.

    
    
        long    x
        long    y   

Then change the values of 10,10 in the gfx.Sprite call to x and y. Spin sets
variables to zero when they're first declared. That means that when your load
this project, your sprite will appear in the top-left corner of the screen,
instead of at (10,10).

    
    
            gfx.Sprite(ball.Addr, x, y, 0)
            lcd.Draw
    

After the repeat, add the ` ctrl.Update ` command. This will check the value
of the pins connected to the joystick. This must be called every time you want
to collect user input.

    
    
        repeat
            ctrl.Update
    
            gfx.Sprite(ball.Addr, x, y, 0)
            lcd.Draw

But it still doesn't do anything! We need to add in some more to tell the game
library what to do when the joystick moves.

    
    
            if ctrl.Right
            	x++
            if ctrl.Left
                x--
            if ctrl.Up
            	y--
            if ctrl.Down
            	y++

**   
|  ![](attachments/13762655/16089165.png)  
---  
  
**

Uh oh. Looks like we just created something that doesn't look like pong at
all, but more like a paint program. That's because we are moving the ball and
redrawing it to the screen below, but everything we drew in the last frame is
still there.

![](attachments/13762655/16089162.png)

Enter the `Clear` function. This paints the screen in the specified
fill colour so that you're starting with a blank canvas with every frame, not
one that's already been drawn on. Let's add `gfx.Fill` just after
`ctrl.Update` , before any other drawing happens.

        repeat
            ctrl.Update
            gfx.Clear

Now you can see that the drawing no longer leaves a trail. Instead, we have a
ball that we can cleanly control, but that still isn't pong, so let's keep
going.

![](attachments/13762655/16089161.png)

** The Story So Far **
    
    
    CON
        _clkmode        = xtal1 + pll16x
        _xinfreq        = 5_000_000
    OBJ
        lcd     : "LameLCD" 
        gfx     : "LameGFX" 
        ctrl    : "LameControl"   
        
        ball    : "gfx_ball"
        paddle  : "gfx_paddle"
    VAR
        long    x
        long    y
        
    PUB Main
        lcd.Start(gfx.Start)
        ctrl.Start
        
        repeat
            ctrl.Update
            gfx.Clear
            if ctrl.Right
            	x++
            if ctrl.Left
                x--
            if ctrl.Up
            	y--
            if ctrl.Down
            	y++
            
            gfx.Sprite(ball.Addr, x, y, 0)
            lcd.Draw


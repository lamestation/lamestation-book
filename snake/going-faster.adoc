== Going Faster

image:fastfastfast.png[]

During the last exercise, you found that as the length of your snake increased, your game actually slowed down. _A lot_.

This may seem surprising, considering that you've probably seen LameStation games where a lot more is happening on the screen and it seems to be running just fine.

That's because as it is, our code is not very efficient.

. LameGFX is designed for drawing full images, so it's not so good at drawing individual pixels.
. Every time the snake moves by one point, it has to move every single dot in its body, one at a time, which takes a long time.
. The snake is moving one pixel at a time, when the dot is 2x2 pixels.

There isn't much we can do about #1, other than write a whole new graphics library (which would take a long time!). #2 can be fixed, but it'll complicate the code, so we'll save that for a rainy day.

For now, let's deal with the third problem, because it's the easiest. If each snake dot is 2x2 pixels, then why not make the snake move two pixels at a time? Otherwise, each image is pointless overlapping the last one which is unnecessary.

=== Instructions

==== Need for speed

As it would happen, there isn't much to do to make this happen. First, we create a constant, just to make our lives easier.

[source, language='con']
.Snake.spin
----
    DOWN  = 2
    LEFT  = 3
    
    MAX_LENGTH = 256
    
+++    SPEED = 2

OBJ
    lcd  : "LameLCD"
----

Now we need to replace every instance of `++` and `--` with `+= SPEED` and `-= SPEED`.

.New Operators: `+=`, `-=`, ...
====
These are shorter ways of writing certain operations that make your code easier to read.
----
x += y    ===>    x := x + y
x -= y    ===>    x := x - y
x *= y    ===>    x := x * y
x /= y    ===>    x := x / y
----
====

Here's what your movement code should look like now.

[source, language='pub']
.Snake.spin
----
            if ctrl.Right
                snakedir := RIGHT
        
        if snakedir == LEFT and snakex[0] > 0
***            snakex[0] -= SPEED
        if snakedir == RIGHT and snakex[0] < constant(128-2)
***            snakex[0] += SPEED
        if snakedir == UP and snakey[0] > 0
***            snakey[0] -= SPEED
        if snakedir == DOWN and snakey[0] < constant(64-2)
***            snakey[0] += SPEED
            
        gfx.Sprite(@dot_gfx, snakex[0], snakey[0], 0)
----

Ahh! Now it goes way, way fast at the beginning! I can hardly even see it. We can fix that, though.

==== Wait for me!

We can make the game more playable by setting the frame rate. That's when we intentionally limit the speed of the game so that it runs at a more consistent speed instead of as fast as it feels like at the moment (a nice thing to have!).

LameLCD has a command just for this: `lcd.SetFrameLimit`. It comes in three speeds.

- `FULLSPEED` (~35 frames/second): Like you're watching TV!
- `HALFSPEED` (~18 frames/second): Starting to look choppy.
- `QUARTERSPEED` (~9fps frames/second): Pretty freaking slow.

You can try different values to see how they look; which one you use really depends on your game.

I'm going to go with `HALFSPEED`. You can call it whenever you like to change the frame rate, but I like to set it once at the beginning to keep it simple.

[source, language='pub']
.Snake.spin
----
PUB Main
    lcd.Start(gfx.Start)
+++    lcd.SetFrameLimit (lcd#HALFSPEED)
    ctrl.Start
----

=== The Code

[source]
.Snake.spin
----
CON
    _clkmode = xtal1 + pll16x
    _xinfreq = 5_000_000

    UP    = 0
    RIGHT = 1
    DOWN  = 2
    LEFT  = 3
    
    MAX_LENGTH = 256
    
    SPEED = 2

OBJ
    lcd  : "LameLCD"
    gfx  : "LameGFX"
    ctrl : "LameControl"
    
VAR
    byte    snakex[MAX_LENGTH]
    byte    snakey[MAX_LENGTH]

    byte    snakedir
    byte    snakecount

    byte    i

PUB Main
    lcd.Start(gfx.Start)
    lcd.SetFrameLimit (lcd#HALFSPEED)
    ctrl.Start
    
    snakex[0] := 32
    snakey[0] := 32
    snakedir := 1
    snakecount := 1
    
    repeat
        gfx.Clear
        ctrl.Update

        if ctrl.A
            if snakecount < constant(MAX_LENGTH-1)
                snakecount++

        if snakedir == LEFT or snakedir == RIGHT
            if ctrl.Up
                snakedir := UP
            if ctrl.Down
                snakedir := DOWN
                
        elseif snakedir == DOWN or snakedir == UP
            if ctrl.Left
                snakedir := LEFT
            if ctrl.Right
                snakedir := RIGHT
        
        if snakedir == LEFT and snakex[0] > 0
            snakex[0] -= SPEED
        if snakedir == RIGHT and snakex[0] < constant(128-2)
            snakex[0] += SPEED
        if snakedir == UP and snakey[0] > 0
            snakey[0] -= SPEED
        if snakedir == DOWN and snakey[0] < constant(64-2)
            snakey[0] += SPEED
            
        gfx.Sprite(@dot_gfx, snakex[0], snakey[0], 0)        

        repeat i from snakecount to 1
            snakex[i] := snakex[i-1]
            snakey[i] := snakey[i-1]
            gfx.Sprite(@dot_gfx, snakex[i], snakey[i], 0) 
            
        lcd.Draw
    
DAT
    dot_gfx
    word    0
    word    2, 2
    word    %%22222211
    word    %%22222211
----

View the complete example at `/tutorials/Snake/GoingFaster.spin`.

=== Think about this!

. What are some other ideas you have to speed up the snake?

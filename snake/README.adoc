= Snake
:experimental:

== The danger zone

So far, the snake is free to come and go as it pleases, criss-crossing over itself like nobody's business. Well, it's time to put a stop to that. Let's show this snake that there are consequences to dilly-dallying.

The change is simple enough. We want the snake to die if it crosses over itself. This can be accomplished by checking each pixel of the snake to see if it overlaps with the head.

----
            if snakex[i] == snakex[0] and snakey[i] == snakey[0]
                snakecount := 1
----

We just need to add it into the loop we've already created that moves the entire snake every frame.

----
        repeat i from snakecount to 1
            if snakex[i] == snakex[0] and snakey[i] == snakey[0]
                snakecount := 1
                snakex[0] := 64
                snakey[0] := 32
                snakedir := 1

            snakex[i] := snakex[i-1]
            snakey[i] := snakey[i-1]
            gfx.Sprite(@dot_gfx, snakex[i], snakey[i], 0)
----

Done. Now your snake will suffer an untimely fate for its carelessness. But we should tie up all its loose ends before it starts a new game.

== The Completed Game

----
CON
    _clkmode = xtal1|pll16x
    _xinfreq = 5_000_000
    
    MAX_SNAKE = 256
    SNAKE_SPEED = 2

OBJ
    lcd  : "LameLCD"
    gfx  : "LameGFX"
    ctrl : "LameControl"
    
VAR    
    byte    snakedir    
    byte    snakex[MAX_SNAKE]
    byte    snakey[MAX_SNAKE]
    byte    snakecount
    
    byte    foodx
    byte    foody
    
    byte    i
    byte    random
    
PUB Main
    lcd.Start(gfx.Start)
    lcd.SetFrameLimit (lcd#HALFSPEED)
    
    random := cnt
    
    foodx := ||random? // 64 * 2
    foody := ||random? // 32 * 2

    snakex[0] := 64
    snakey[0] := 32
    snakedir := 1
    snakecount := 1

    repeat        
        gfx.Clear
        ctrl.Update
        
        if snakedir == 0 or snakedir == 2
            if ctrl.Left
                snakedir := 3
            elseif ctrl.Right
                snakedir := 1

        elseif snakedir == 1 or snakedir == 3
            if ctrl.Up
                snakedir := 0
            elseif ctrl.Down
                snakedir := 2


        if snakedir == 3
            if snakex[0] > 0
                snakex[0] -= SNAKE_SPEED
                
        if snakedir == 1
            if snakex[0] < constant(128-2)
                snakex[0] += SNAKE_SPEED

        if snakedir == 0
            if snakey[0] > 0
                snakey[0] -= SNAKE_SPEED
                
        if snakedir == 2
            if snakey[0] < constant(64-2)
                snakey[0] += SNAKE_SPEED

        
        if foodx == snakex[0] and foody == snakey[0]
            foodx := ||random? // 64 * 2
            foody := ||random? // 32 * 2

            if snakecount < constant(MAX_SNAKE-1)
                snakecount++

        gfx.Sprite(@food_gfx, foodx, foody, 0) 

    
        repeat i from snakecount to 1
            if snakex[i] == snakex[0] and snakey[i] == snakey[0]
                snakecount := 1
                snakex[0] := 64
                snakey[0] := 32
                snakedir := 1

            snakex[i] := snakex[i-1]
            snakey[i] := snakey[i-1]
            gfx.Sprite(@dot_gfx, snakex[i], snakey[i], 0)
            
        gfx.Sprite(@dot_gfx, snakex[0], snakey[0], 0) 
        
        lcd.DrawScreen

DAT

    dot_gfx
    word    0
    word    2, 2
    word    %%22222211
    word    %%22222211

    food_gfx
    word    0
    word    2, 2
    word    %%22222233
    word    %%22222233
----

== Going further

There's clearly a lot more that could be done. You could add walls to crash into, fancier graphics, a snake that looks and feels like a real snake, the world is your oyster. What would you do next?

- Cleaning up the code


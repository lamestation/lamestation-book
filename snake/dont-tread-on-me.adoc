== Don't Tread On Me

So far, the snake is free to come and go as it pleases, criss-crossing over itself like nobody's business. Well, it's time to put a stop to that. Let's show this snake that there are consequences to dilly-dallying.

=== Instructions

The change is simple enough. We want the snake to die if it crosses over itself. This can be accomplished by checking each pixel of the snake to see if it overlaps with the head.

[source, language='pub']
----
            if snakex[i] == snakex[0] and snakey[i] == snakey[0]
----

Then if true, we make sure to tie up all the loose ends, so that the snake starts over fresh.

[source, language='pub']
----
            if snakex[i] == snakex[0] and snakey[i] == snakey[0]
                snakecount := 1
                snakex[0] := 64
                snakey[0] := 32
                snakedir := 1
----

We just need to add it into the loop we've already created that moves the entire snake every frame.

[source, language='pub']
.Snake.spin
----
        gfx.Sprite(@dot_gfx, snakex[0], snakey[0], 0)        

        repeat i from snakecount to 1
+++            if snakex[i] == snakex[0] and snakey[i] == snakey[0]
+++                snakecount := 1
+++                snakex[0] := 64
+++                snakey[0] := 32
+++                snakedir := 1

            snakex[i] := snakex[i-1]
            snakey[i] := snakey[i-1]
            gfx.Sprite(@dot_gfx, snakex[i], snakey[i], 0)
            
        lcd.Draw
----

Done. Now your snake will suffer an untimely fate for its carelessness.

=== The Code

[source]
.Snake.spin
----
CON
    _clkmode = xtal1 + pll16x
    _xinfreq = 5_000_000

    UP    = 0
    RIGHT = 1
    DOWN  = 2
    LEFT  = 3
    
    MAX_LENGTH = 256
    
    SPEED = 2

OBJ
    lcd  : "LameLCD"
    gfx  : "LameGFX"
    ctrl : "LameControl"
    
VAR
    byte    snakex[MAX_LENGTH]
    byte    snakey[MAX_LENGTH]

    byte    snakedir
    byte    snakecount
    
    byte    foodx
    byte    foody

    byte    random
    byte    i

PUB Main
    lcd.Start(gfx.Start)
    lcd.SetFrameLimit (lcd#HALFSPEED)
    ctrl.Start

    random := cnt
    
    snakex[0] := 32
    snakey[0] := 32
    snakedir := 1
    snakecount := 1

    foodx := ||random? // 64 * 2
    foody := ||random? // 32 * 2
    
    repeat
        gfx.Clear
        ctrl.Update

        if foodx == snakex[0] and foody == snakey[0]
            if snakecount < constant(MAX_LENGTH-1)
                snakecount++

            foodx := ||random? // 64 * 2
            foody := ||random? // 32 * 2
        
        gfx.Sprite(@food_gfx, foodx, foody, 0)

        if snakedir == LEFT or snakedir == RIGHT
            if ctrl.Up
                snakedir := UP
            if ctrl.Down
                snakedir := DOWN
                
        elseif snakedir == DOWN or snakedir == UP
            if ctrl.Left
                snakedir := LEFT
            if ctrl.Right
                snakedir := RIGHT
        
        if snakedir == LEFT and snakex[0] > 0
            snakex[0] -= SPEED
        if snakedir == RIGHT and snakex[0] < constant(128-2)
            snakex[0] += SPEED
        if snakedir == UP and snakey[0] > 0
            snakey[0] -= SPEED
        if snakedir == DOWN and snakey[0] < constant(64-2)
            snakey[0] += SPEED
            
        gfx.Sprite(@dot_gfx, snakex[0], snakey[0], 0)        

        repeat i from snakecount to 1
            if snakex[i] == snakex[0] and snakey[i] == snakey[0]
                snakecount := 1
                snakex[0] := 64
                snakey[0] := 32
                snakedir := 1

            snakex[i] := snakex[i-1]
            snakey[i] := snakey[i-1]
            gfx.Sprite(@dot_gfx, snakex[i], snakey[i], 0)
            
        lcd.Draw
    
DAT
    dot_gfx
    word    0
    word    2, 2
    word    %%22222211
    word    %%22222211
    
    food_gfx
    word    0
    word    2, 2
    word    %%22222233
    word    %%22222233
----

View this example at `/tutorials/Snake/DontTreadOnMe.spin`.

=== Think about this!

. We wrote code so that the snake dies when it overlaps itself, but why does it die when it reaches the edge of the screen?

// that's because when it can't go any further, it stays put, so the next pixel will come and overlap it on the next frame. This snake essentially has to keep moving or will die.
